// -*- ASCII:EDT -*-
// TeX: MetaFont ...

  Requires(20110812); // Requires this build of WinEdt to work properly

  PushTagsandRegisters;

  // Delete .mft file...
    IfFileExists("%O\%N.mft",!"DeleteFile('%O\%N.mft')");

  Assign('ExitCode-OK','0'); // OK Exit Code
  LetReg(9,'MetaFont'); // Execution Modes Compiler
  LetReg(8,'log');      // Log File (or empty)
  LetReg(7,'');         // Output Format
  LetReg(6,'%O');       // Startup Folder

  // Display a Dialog for Extra Switches:
  IfStr("%$('%!9-GUI');","1","=",>
    !|GetString("Switches: ","%!9 Switches","%$('%!9--Switches');");>
      IfOK(!`Assign('%!9--Switches','%!?');`,!`PopTagsandRegisters;Exit;`);|,>
    !|Release('%!9--Switches');|);
  // Non-Stop Mode?
  IfStr("%$('%!9-No_Interaction');","1",!"=",>
    !"LetReg(3,'--interaction=nonstopmode');",!"LetReg(3,'--interaction=errorstopmode');");
  // C Style Errors?
  IfStr("%$('%!9-C-style-errors');","1",!"=",!"LetReg(3,'%!3 --file-line-error');");
  // Build the Command Line ...
  LetReg(5, "%$('%!9-Executable'); %$('%!9-Switches'); %!3 %$('%!9--Switches'); %$('%!9-Parameters');");

  // Use Execution Modes to determine the flow of execution...
  Exe('%b\Exec\ExeCompiler.edt');

  // Create a .mft file if any output was generated...
    ListFolderFiles(0,'%O','',0,1);
    LstEnum('%!0','%\');
    LstGetCount(1);
    Repeat(%!1,>
      !`LstGetItem(2,%!c-1);>
        GetFileName('%!2',3);>
        GetFileType('%!2',4);>
        FindInString('%!4','<\.+$Numeric$gf~$~[]$',5,6,1);>
        IfOK(!|IfStr('%!3','%N','=',!'WriteFile("%O\%N.mft","");Break;');|);>
        ProcessMessages;>
       `);
    LstRelease;

  PopTagsandRegisters;

End;
